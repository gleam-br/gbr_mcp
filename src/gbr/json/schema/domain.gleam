////
////
////

import gleam/dict.{type Dict}
import gleam/list
import gleam/option.{type Option, None, Some}

import non_empty_list.{type NonEmptyList}

import gbr/shared/utils

/// Node in the Specification that might be represented by a reference.
pub type Ref(t) {
  Ref(ref: String, summary: Option(String), description: Option(String))
  Inline(value: t)
}

/// Represents a decoded JSON schema.
///
/// Chosen to add metadata inline as it doesn't belong on ref object
/// https://json-schema.org/draft/2020-12/json-schema-validation#name-a-vocabulary-for-basic-meta
///
pub type Schema {
  Boolean(
    nullable: Bool,
    title: Option(String),
    description: Option(String),
    deprecated: Bool,
  )
  Integer(
    multiple_of: Option(Int),
    maximum: Option(Int),
    exclusive_maximum: Option(Int),
    minimum: Option(Int),
    exclusive_minimum: Option(Int),
    nullable: Bool,
    title: Option(String),
    description: Option(String),
    deprecated: Bool,
  )
  Number(
    multiple_of: Option(Int),
    maximum: Option(Int),
    exclusive_maximum: Option(Int),
    minimum: Option(Int),
    exclusive_minimum: Option(Int),
    nullable: Bool,
    title: Option(String),
    description: Option(String),
    deprecated: Bool,
  )
  String(
    max_length: Option(Int),
    min_length: Option(Int),
    pattern: Option(String),
    // There is an enum of accepted formats but it is extended by OAS spec.
    format: Option(String),
    nullable: Bool,
    title: Option(String),
    description: Option(String),
    deprecated: Bool,
  )
  Null(title: Option(String), description: Option(String), deprecated: Bool)
  Array(
    max_items: Option(Int),
    min_items: Option(Int),
    unique_items: Bool,
    items: Ref(Schema),
    nullable: Bool,
    title: Option(String),
    description: Option(String),
    deprecated: Bool,
  )
  Object(
    properties: Dict(String, Ref(Schema)),
    required: List(String),
    additional_properties: Option(Ref(Schema)),
    max_properties: Option(Int),
    // "Omitting this keyword has the same behavior as a value of 0"
    min_properties: Int,
    nullable: Bool,
    title: Option(String),
    description: Option(String),
    deprecated: Bool,
  )
  AllOf(NonEmptyList(Ref(Schema)))
  AnyOf(NonEmptyList(Ref(Schema)))
  OneOf(NonEmptyList(Ref(Schema)))
  Enum(NonEmptyList(utils.Any))
  AlwaysPasses
  AlwaysFails
}

/// Construct a schema term requiring a boolean value.
pub fn boolean() {
  Boolean(nullable: False, title: None, description: None, deprecated: False)
}

/// Construct a schema term requiring an integer value.
pub fn integer() {
  Integer(
    multiple_of: None,
    maximum: None,
    exclusive_maximum: None,
    minimum: None,
    exclusive_minimum: None,
    nullable: False,
    title: None,
    description: None,
    deprecated: False,
  )
}

/// Construct a schema term requiring a float value.
pub fn number() {
  Number(
    multiple_of: None,
    maximum: None,
    exclusive_maximum: None,
    minimum: None,
    exclusive_minimum: None,
    nullable: False,
    title: None,
    description: None,
    deprecated: False,
  )
}

/// Construct a schema term requiring a string value.
pub fn string() {
  String(
    max_length: None,
    min_length: None,
    pattern: None,
    format: None,
    nullable: False,
    title: None,
    description: None,
    deprecated: False,
  )
}

/// Construct a schema term requiring a null value.
pub fn null() {
  Null(title: None, description: None, deprecated: False)
}

/// Construct a schema term requiring an array value.
pub fn array(items) {
  Array(
    max_items: None,
    min_items: None,
    unique_items: False,
    items:,
    nullable: False,
    title: None,
    description: None,
    deprecated: False,
  )
}

/// Create a schema for an object with all properties defined.
///
/// user `field` and `optional_field` to define fields.
pub fn object(properties) {
  let #(properties, required) =
    list.fold(properties, #(dict.new(), []), fn(acc, property) {
      let #(properties, all_required) = acc
      let #(key, schema, is_required) = property
      let all_required = case is_required {
        True -> [key, ..all_required]
        False -> all_required
      }
      let properties = dict.insert(properties, key, schema)
      #(properties, all_required)
    })
  Object(
    properties:,
    required:,
    additional_properties: None,
    max_properties: None,
    min_properties: 0,
    nullable: False,
    title: None,
    description: None,
    deprecated: False,
  )
}

/// Create a schema for an open dictionary of strings to the given schema type
pub fn dict(field_schema) {
  Object(
    properties: dict.new(),
    required: [],
    additional_properties: Some(Inline(field_schema)),
    deprecated: False,
    max_properties: None,
    min_properties: 0,
    nullable: False,
    title: None,
    description: None,
  )
}

pub fn enum_of_strings(strings) {
  Enum(non_empty_list.map(strings, utils.String))
}

/// Construct a schema for a required field of an object.
pub fn field(key, schema) {
  #(key, Inline(schema), True)
}

/// Construct a schema for an optional field of an object.
pub fn optional_field(key, schema) {
  #(key, Inline(schema), False)
}

/// encode a schema to the fields of the json object.
/// A schema will always be an object
pub fn to_fields(schema) {
  case schema {
    Boolean(nullable:, title:, description:, deprecated:) ->
      any_object([
        #("type", Some(utils.String("boolean"))),
        #("nullable", Some(utils.Boolean(nullable))),
        #("title", option.map(title, utils.String)),
        #("description", option.map(description, utils.String)),
        #("deprecated", Some(utils.Boolean(deprecated))),
      ])
    Integer(..) as int -> {
      any_object([
        #("type", Some(utils.String("integer"))),
        #("multipleOf", option.map(int.multiple_of, utils.Integer)),
        #("maximum", option.map(int.maximum, utils.Integer)),
        #("exclusiveMaximum", option.map(int.exclusive_maximum, utils.Integer)),
        #("minimum", option.map(int.minimum, utils.Integer)),
        #("exclusiveMinimum", option.map(int.exclusive_minimum, utils.Integer)),
        #("nullable", Some(utils.Boolean(int.nullable))),
        #("title", option.map(int.title, utils.String)),
        #("description", option.map(int.description, utils.String)),
        #("deprecated", Some(utils.Boolean(int.deprecated))),
      ])
    }
    Number(..) as number -> {
      any_object([
        #("type", Some(utils.String("number"))),
        #("multipleOf", option.map(number.multiple_of, utils.Integer)),
        #("maximum", option.map(number.maximum, utils.Integer)),
        #(
          "exclusiveMaximum",
          option.map(number.exclusive_maximum, utils.Integer),
        ),
        #("minimum", option.map(number.minimum, utils.Integer)),
        #(
          "exclusiveMinimum",
          option.map(number.exclusive_minimum, utils.Integer),
        ),
        #("nullable", Some(utils.Boolean(number.nullable))),
        #("title", option.map(number.title, utils.String)),
        #("description", option.map(number.description, utils.String)),
        #("deprecated", Some(utils.Boolean(number.deprecated))),
      ])
    }
    String(..) as string -> {
      any_object([
        #("type", Some(utils.String("string"))),
        #("MaxLength", option.map(string.max_length, utils.Integer)),
        #("MinLength", option.map(string.min_length, utils.Integer)),
        #("Pattern", option.map(string.pattern, utils.String)),
        #("Format", option.map(string.format, utils.String)),
        #("nullable", Some(utils.Boolean(string.nullable))),
        #("title", option.map(string.title, utils.String)),
        #("description", option.map(string.description, utils.String)),
        #("deprecated", Some(utils.Boolean(string.deprecated))),
      ])
    }
    Null(..) as null -> {
      any_object([
        #("type", Some(utils.String("null"))),
        #("title", option.map(null.title, utils.String)),
        #("description", option.map(null.description, utils.String)),
        #("deprecated", Some(utils.Boolean(null.deprecated))),
      ])
    }
    Array(..) as array -> {
      any_object([
        #("type", Some(utils.String("array"))),
        #("maxItems", option.map(array.max_items, utils.Integer)),
        #("minItems", option.map(array.min_items, utils.Integer)),
        #("uniqueItems", Some(utils.Boolean(array.unique_items))),
        #("items", Some(ref_to_fields(array.items))),
        #("nullable", Some(utils.Boolean(array.nullable))),
        #("title", option.map(array.title, utils.String)),
        #("description", option.map(array.description, utils.String)),
        #("deprecated", Some(utils.Boolean(array.deprecated))),
      ])
    }
    Object(..) as object -> {
      any_object([
        #("type", Some(utils.String("object"))),
        #(
          "properties",
          Some(utils.Object(
            object.properties |> dict.map_values(fn(_, v) { ref_to_fields(v) }),
          )),
        ),
        #(
          "additionalProperties",
          option.map(object.additional_properties, ref_to_fields),
        ),
        #("maxProperties", option.map(object.max_properties, utils.Integer)),
        #("minProperties", Some(utils.Integer(object.min_properties))),
        #(
          "required",
          Some(utils.Array(list.map(object.required, utils.String))),
        ),
        #("nullable", Some(utils.Boolean(object.nullable))),
        #("title", option.map(object.title, utils.String)),
        #("description", option.map(object.description, utils.String)),
        #("deprecated", Some(utils.Boolean(object.deprecated))),
      ])
    }
    AllOf(varients) -> {
      any_object([
        #(
          "allOf",
          Some(
            utils.Array(list.map(
              non_empty_list.to_list(varients),
              ref_to_fields,
            )),
          ),
        ),
      ])
    }
    AnyOf(varients) -> {
      any_object([
        #(
          "anyOf",
          Some(
            utils.Array(list.map(
              non_empty_list.to_list(varients),
              ref_to_fields,
            )),
          ),
        ),
      ])
    }
    OneOf(varients) -> {
      any_object([
        #(
          "oneOf",
          Some(
            utils.Array(list.map(
              non_empty_list.to_list(varients),
              ref_to_fields,
            )),
          ),
        ),
      ])
    }
    Enum(non_empty_list.NonEmptyList(value, [])) ->
      any_object([#("const", Some(value))])
    Enum(values) -> {
      let values = non_empty_list.to_list(values)
      any_object([#("enum", Some(utils.Array(values)))])
    }
    AlwaysPasses ->
      // These can't be turned to fields
      panic as "utils.Boolean(True)"
    AlwaysFails -> panic as "utils.Boolean(False)"
  }
}

pub fn ref_to_fields(ref) {
  case ref {
    Inline(schema) -> to_fields(schema) |> utils.Object
    Ref(reference, ..) ->
      utils.Object(dict.from_list([#("$ref", utils.String(reference))]))
  }
}

pub fn any_object(properties) {
  list.filter_map(properties, fn(property) {
    let #(key, value) = property
    case value {
      Some(value) -> Ok(#(key, value))
      None -> Error(Nil)
    }
  })
  |> dict.from_list
}
